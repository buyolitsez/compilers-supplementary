-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}

fun addDefinitions(state, defs) {
  foldl(fun (cstate, def) {
    case def of
        Var(x)                -> addNames(cstate, x)
      | Fun(name, args, body) -> addFunction(cstate, name, args, body)
    esac
  }, state, defs)
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)
fun eval (c@[st, w], expr) {
  case expr of
      Assn (exL, exR)              -> case evalList(c, { exL, exR }) of
        [c@[st, w], { n, v }] -> [[st <- [n, v], w], v]
      esac
    | Seq (ex1, ex2)               -> case evalList(c, { ex1, ex2 }) of
      [c, { _, v }] -> [c, v]
    esac
    | Skip                         -> [c, Void]
    | Read (n)                     -> case readWorld(w) of
      [v, wf] -> [[st <- [n, v], wf], Void]
    esac
    | Write (ex)                   -> case eval(c, ex) of
      [[st, w], v] -> [[st, writeWorld(v, w)], Void]
    esac
    | If(exIf, exTrue, exFalse)    -> case eval(c, exIf) of
        [c, 0] -> eval(c, exFalse)
      | [c, _] -> eval(c, exTrue)
    esac
    | cycle@(While (exIf, exBody)) -> case eval(c, exIf) of
        [c, 0] -> [c, Void]
      | [c, _] -> case eval(c, exBody) of
        [c, _] -> eval(c, cycle)
      esac
    esac
    | DoWhile (exBody, exIf)       -> case eval(c, exBody) of
      [c, _] -> eval(c, While(exIf, exBody))
    esac
    | Var(v)                       -> [c, lookup(st, v)]
    | Ref (x)                      -> [c, x]
    | Const (n)                    -> [c, n]
    | Binop (op, ex1, ex2)         -> case evalList(c, { ex1, ex2 }) of
      [cc, { a, b }] -> [cc, evalOp(op, a, b)]
    esac
    | Ignore (ex)                  -> case eval(c, ex) of
      [c, _] -> [c, Void]
    esac
    | Scope(defs, expr)            -> case enterScope(st) of
      st -> case addDefinitions(st, defs) of
        st -> case eval([st, w], expr) of
          [[st, w], value] -> [[leaveScope(st), w], value]
        esac
      esac
    esac
    | Call(name, args)             -> case lookup(st, name) of
      Fun(funargs, body) -> case evalList(c, args) of
        [[stBefore, w], vals] -> case enterFunction(stBefore) of
          stAfter -> case foldl(fun (st, [ref, value]) {
            addName(st, ref, value)
          }, stAfter, zip(funargs, vals)) of
            stAfter -> case eval([stAfter, w], body) of
              [[stAfter, w], value] -> [[leaveFunction(stBefore, getGlobal(stAfter)), w], value]
            esac
          esac
        esac
      esac
    esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
